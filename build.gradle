buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.5'
    }
}

plugins {
  id "com.google.protobuf" version "0.8.5"
  id "java"
  id "application"
  id "idea"
  id 'com.github.ben-manes.versions' version '0.19.0'
}

group 'io.github.caio.grpc'
version '1.0-SNAPSHOT'

protobuf {
    protoc {
        // The version of protoc must match protobuf-java. If you don't depend on
        // protobuf-java directly, you will be transitively depending on the
        // protobuf-java version that grpc depends on.
        artifact = "com.google.protobuf:protoc:3.6.0"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.12.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
    generatedFilesBaseDir = "$projectDir/src/generated"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile 'io.grpc:grpc-all:1.12.0'
    compile 'javax.annotation:javax.annotation-api:1.2'
}

idea {
    module {
        sourceDirs += file("${projectDir}/src/generated/main/java");
        sourceDirs += file("${projectDir}/src/generated/main/grpc");
    }
}

task runServer(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'be.msec.labgrpc.ServerScreen'
}

task runClient(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'be.msec.labgrpc.ClientScreen'
}

startScripts.enabled = false

task chatroomServer(type: CreateStartScripts) {
    mainClassName = "be.msec.grpclab.ChatroomServer"
    applicationName = "chat-server"
    outputDir = new File(project.buildDir, 'tmp')
    classpath = jar.outputs.files + project.configurations.runtime
}

task chatroomClient(type: CreateStartScripts) {
    mainClassName = "be.msec.grpclab.ChatroomClient"
    applicationName = "chat-client"
    outputDir = new File(project.buildDir, 'tmp')
    classpath = jar.outputs.files + project.configurations.runtime
}

applicationDistribution.into("bin") {
    from(chatroomServer)
    from(chatroomClient)
    fileMode = 0755
}
